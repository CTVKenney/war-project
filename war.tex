\documentclass[12pt]{amsart}
\usepackage{epigraph}
\usepackage[]{filecontents}
\usepackage[]{lettrine}
\usepackage[]{graphicx}
\usepackage[]{listings}
\usepackage[]{xcolor}
\usepackage[]{biblatex}
\usepackage[]{hyperref}

\setlength\epigraphwidth{10cm}

\lstdefinelanguage{Maple}% 
{morekeywords={and,assuming,break,by,catch,description,do,done,% 
elif,else,end,error,export,fi,finally,for,from,global,if,% 
implies,in,intersect,local,minus,mod,module,next,not,od,% 
option,options,or,proc,quit,read,return,save,stop,subset,then,% 
to,try,union,use,uses,while,xor,RETURN},% 
sensitive=true,% 
morecomment=[l]\#,% 
morestring=[d]{"'`},% 
morestring=[d]"% 
}[keywords,comments,strings]% 

% See: https://en.wikibooks.org/wiki/LaTeX/Source_Code_Listings
\lstdefinestyle{maple}{
  belowcaptionskip=1\baselineskip,
  breaklines=true,
  frame=single,
  language=Maple,
  xleftmargin=\parindent,
  showstringspaces=false,
  basicstyle=\small\ttfamily,
  keywordstyle=\bfseries\color{green!60!black},
  commentstyle=\itshape\color{purple!40!black},
  identifierstyle=\color{black},
  stringstyle=\color{orange},
}

\lstset{escapechar=@,style=maple, numbers=left}

\begin{filecontents}{cite.bib}
@book{hargrave,
  title={{A History of Playing Cards and a Bibliography of Cards and Gaming}},
  author={Hargrave, Catherine Perry},
  year={2000},
  publisher={Courier Corporation}
}
\end{filecontents}

\addbibresource{cite.bib}

\title{The Game of War}
\author{Alison Bu, Robert Dougherty-Bliss, Charles Kenney}
\date{\today}


\begin{document}


\maketitle


\epigraph{\emph{Primitive man tossed his arrows into the magic ring which he had
    drawn on the ground with the proper rites and incantations. And who can say
    that some power higher than the stars did not direct their flight, and
bring him in safety to his journey's end?}}{--- Catherine Hargrave, \emph{A History of Playing Cards}}

\section{Introduction}%
\label{sec:introduction}

\lettrine[lraise=0.16]{W}{e, the war team,} were assigned to study the game of
War, a classic, internationally-known card game. To this end, we have written a
comprehensive collection of Maple code to simulate and analyze War and various
generalizations we have considered.

The main items of our report are as follows:

\begin{itemize}
    \item A large body of Maple code.
    \item Pictures of the ``War graph'' for small hands.
    \item A definition of the sequence, ``longest possible game of War on the
        deck $[n] = \{1, 2, \dots, n\}$.''
\end{itemize}

Our report is structured into three parts. Section~\ref{sec:history} contains
our (failed) attempt to reconstruct the history of War\footnotemark.
Section~\ref{sec:procedures} contains all of our Maple code.
Section~\ref{sec:graph} contains some notes on the ``War graph'' as well as
some pictures we made.

\footnotetext{The game, of course.}

\section{A History of Cards}
\label{sec:history}

% I don't know why I'm writing this section, really. It just seemed interesting,
% and I couldn't find anything about the history of war.


% Not a very convincing paragraph.


The game of war seems nearly universal. In America, we play \emph{War}; in
England, they call it \emph{Battle}; the Germans have a varient called
\emph{Tod und Leben}. Despite its popularity, the origins of War are completely
mysterious. There are references to \emph{Tod und Leben} found in nineteenth
century game books, but surely the game of war existed before this in different
forms. Lacking any authoritative history on the subject, it seems just as good
to talk about the history of playing cards themselves, and perhaps surmise when
War \emph{could} have been invented based on this.

Playing cards have surely existed in some form for much longer than we have
records of them. Stewart Culin, the great American game expert and early
ethnographer, believed that playing cards descended from divination rituals,
which existed in nearly every culture around the world: ``In order to classify
objects and events which did not in themselves reveal their proper assignment,
resort was had to magic. Our present games are the survivals of these magical
processes'' \cite[p.~1]{hargrave}.

Nonetheless, the earliest \emph{known} playing cards are Chinese and from the
twelfth century. The cards were copied from Chinese notes invented in the Tang
Dynasty (est.~618 CE)--- gamblers likely played with the notes themselves.
Other popular cards, used interchangeably, were \emph{domino cards}, cards with
patterns of dots added for significance. These are quite beautiful---see
Figure~\ref{fig:domino}.

\begin{figure}[htpb]
    \centering
    \includegraphics[width=0.8\linewidth]{domino}
    \caption{Chinese domino cards depicting characters from the story of the
    river banks. From \cite{hargrave}.}
    \label{fig:domino}
\end{figure}


It is unclear how or in what form playing cards first spread to Europe. One
popular theory asserts that ``wandering gypsies'' brought them from India.
Culin believed the opposite, that Indian playing cards were inspired by early
European developments. However they arrived, the modern playing card was
developed in France.


The first French playing cards were tarot cards---still available
today---painted for King Charles VI in 1392 CE, and popular among
working-people no later than 1397 CE. Tarot cards come in collections of
twenty-two, painted with figures and scenes which hold some significance, such
as \emph{Le bateleur} (``The magician''), \emph{Le chariot}, and \emph{La
justice}. The cards begin with suites of Cups, Swords, Coins, and Batons, but
were changed to Hearts, Clubs, Spades, and Diamonds when, according to legend,
the famous Hundred Years' War commander La Hire invented the game of piquet. At
this time the cards were numbered linearly \emph{by suite}. Around 1440, the
first ``court'' (face) cards were named. By 1516 the standard 52-card deck had
been invented. These are essentially the cards that we still use today, some
half-a-millennium later.

In summary, playing cards with numeric values have been around for hundreds of
years. Some of the earliest games we have records of are already more
complicated than War. It seems difficult to pinpoint exactly when the game
started, but it must have been well-over two-hundred years ago, if not
\emph{significantly} further in our history.

\section{Procedures}
\label{sec:procedures}
\subsection{Setting Up the Game}


\subsubsection{Distributing the Deck} \hfill


The procedure \textbf{RanDeck(n,k,r)} inputs positive integers $n,k,$ and $r$. It simulates dealing out a randomly shuffled deck of cards containing $k$ copies of $n$ unique, ordered cards to $r$ players.\footnote{In other words, $kn$ cards with $k$ suits.}
 The maximal number of cards such that each player has the same number of cards are dealt out and any remaining cards are removed from the game.


\textbf{RanDeck(n,k,r)} partitions the first $\lfloor\frac{n*k}{r}\rfloor *r $ entries of a random permutation of $[ \underbrace{1,...,1}_{r \text{ copies}},...,\underbrace{n,...,n}_{r \text{ copies}}]$ by creating a new part after every $\lfloor \frac{n*k}{r} \rfloor$-th entry. It outputs this partition as a list of length $r$, where the $i$-th entry is a list that represents the $i$-th players deck.


\begin{lstlisting}
RanDeck:=proc(n,k,r) local HandSize, i, pi, Deck, decks, discard:
 Deck:=[seq(seq(i,j=1..k),i=1..n)]:
 Deck:=randperm(Deck):
 HandSize:=floor(n*k/r):
 decks:=[seq([seq(Deck[i],i=(j-1)*HandSize+1..j*HandSize)],j=1..r)]:
 end:
\end{lstlisting}


\subsubsection{Defining War}\hfill


A war occurs when the highest card played in a round is not unique. In a war, players place $m$ cards face down and then play their next card. The following three procedures define different ways to complete a war when a player does not have enough cards.  Each of the following procedures input a non-negative integer $m$, a list $Q$, and a positive integer $r$, where $Q[i]$ represents the $i$-th player's deck and $|Q|=r$.


The procedure \textbf{WARm(m,Q,r)} simulates a war where $m$ cards must be placed face down, and any player that does not have enough cards automatically loses. If no player has enough cards to complete the war, it outputs $\{0\}$ to indicate that the game will have no winners. Otherwise, it outputs a list $[tempQ,b]$, where $tempQ$ is a list of each players deck after placing $m$ cards face down and $b$ is a list of the cards placed face down or played during the war.


\begin{lstlisting}
WARm:=proc(m,Q,r) local HandSize, tempQ,i,output,a,b:
  HandSize:=[seq(nops(Q[i]), i=1..r)]:
  if max(HandSize)>m then tempQ:=Q: a:=Q:
  for i from 1 to r do 
    if HandSize[i]<m+1 then tempQ[i]:=[]:  a[i]:=[op(1..-1,Q[i]),seq(0,j=HandSize[i]..m)]: 
    else tempQ[i]:=[op(m+1..-1,Q[i])]: a[i]:=[op(1..m,Q[i])]: 
    fi: od: 
  b:=remove(t->t=0,[seq(seq(a[i][j],i=1..r),j=1..m)]):
  output:=[tempQ,b]:
  else output:={0}: fi:
  output:
 end:
\end{lstlisting}




The procedure \textbf{WARmin(m,Q,r)} inputs a non-negative integer $m$, a list $Q$, and a positive integer $r$ such that $|Q|=r$.  It simulates a war where $Q[i]$ represents the $i$-th player's deck, and $m$ cards must be placed face down unless any player still in the game does not have enough cards to do so.  If the smallest hand at the start of the war is $k<m+1$, then every player places $k-1$ cards face down and plays their next card.  If no player has any remaining cards, it outputs 0 to indicate that the game will end in a tie. Otherwise, it outputs a list $[tempQ,b]$, where $tempQ$ is a list of each players deck after placing their cards face down and $b$ is a list of the cards placed face down or played during the war.


\begin{lstlisting}
WARmin:=proc(m,Q,r) local HandSize, hs, tempQ,i,output:
  HandSize:=[seq(nops(Q[i]),i=1..r)]
  if add(HandSize[i],i=1..r)<1 then output:=0:
  else hs:=min(remove(t->t=0,HandSize)):
    if hs>m then output:=WARm(m,Q,r):
    else output:=WARm(hs-1,Q,r):
    fi:
  fi:  
  output:
 end:
\end{lstlisting}




The procedure \textbf{WARmax(m,Q,r)} inputs a non-negative integer $m$, a list $Q$, and a positive integer $r$ such that $|Q|=r$.  It simulates a war where the $i$-th entry of $Q$ represents the $i$-th player's deck, and $m$ cards must be placed face down unless no player still in the game has enough cards to do so.  If the largest hand at the start of the war is $k<m+1$, then every player places $k-1$ cards face down and plays their next card. Any player that does not have nough cards to do so automatically loses.  If no player has any remaining cards, it outputs 0 to indicate that the game will end in a tie. Otherwise, it outputs a list $[tempQ,b]$, where $tempQ$ is a list of each players deck after placing their cards face down and $b$ is a list of the cards placed face down or played during the war.


\begin{lstlisting}
WARmax:=proc(m,Q,r) local HandSize,HS, tempQ,i,output:
  HandSize:=[seq(nops(Q[i]),i=1..r)]:
  HS:=max(HandSize):
  if HS>m then output:=WARm(m,Q,r):
   elif HS=0 then output:=0:
   else output:=WARm(HS-1,Q,r):
   fi:
  output:
end: 
\end{lstlisting}




\subsubsection{Defining Replacement}\hfill


The following procedures define how cards are placed back into the deck after winning a round. Each procedure inputs a list $Q$, a positive integer $r$ where $|Q|=r$, a list $b$, and a positive integer $N$. The $i$-th entry of $Q$ represents the $i$-th player's deck, $b$ lists the cards on the table in the order they were played where Player $i$ is the $i$-th player to play a card (i.e. $b$ lists the cards that the winner of the round will take), and Player $N$ is the winner of the round.


The procedure \textbf{RepPlayed(Q,r,b,N)} outputs a list of each players hand after adding the won cards to the bottom of Player $N$'s deck in the order that they were played.


\begin{lstlisting}
RepPlayed:=proc(Q,r,b,N) local c:
  c:=remove(t->t=0,b):
  [seq(Q[i],i=1..N-1),[op(Q[N]),op(c)],seq(Q[i],i=N+1..r)]:
 end:
\end{lstlisting}


The procedure \textbf{RepAllAsc(Q,r,b,N)} outputs a list of each players hand after sorting all the won cards in ascending order and then placing them at the bottom of Player $N$'s deck.


\begin{lstlisting}
RepAllAsc:=proc(Q,r,b,N) local c:
  c:=remove(t->t=0,b):
  c:=sort(c):
  [seq(Q[i],i=1..N-1),[op(Q[N]),op(c)],seq(Q[i],i=N+1..r)]:
end:
\end{lstlisting}


The procedure \textbf{RepAllDesc(Q,r,b,N)} outputs a list of each players hand after sorting all the won cards in descending order and then placing them at the bottom of Player $N$'s deck.




\begin{lstlisting}
RepAllDesc:=proc(Q,r,b,N) local c:
  c:=remove(t->t=0,b):
  c:=sort(b,`>`):
  [seq(Q[i],i=1..N-1),[op(Q[N]),op(c)],seq(Q[i],i=N+1..r)]:
end:
\end{lstlisting}




The procedure \textbf{RepAsc(Q,r,b,N)} outputs a list of each players hand after sorting each subround (i.e. each time all the players placed a card on the table) in ascending order and then placing them at the bottom of Player N's deck. In a war, the cards that each player placed face down first are sorted in ascending order, then then the card that each player put down next are sorted in ascending order, and so on.




\begin{lstlisting}
RepAsc:=proc(Q,r,b,N) local n,c:
  n:=nops(b)/r:
  c:=[seq(op(sort([op(i*r+1..i*r+r,b)])),i=0..n-1)]:
  c:=remove(t->t=0,c):
  [seq(Q[i],i=1..N-1),[op(Q[N]),op(c)],seq(Q[i],i=N+1..r)]:
end:
\end{lstlisting}


The procedure \textbf{RepDesc(Q,r,b,N)} outputs a list of each players hand after sorting each subround (i.e. each time all the players placed a card on the table) in descending order and then placing them at the bottom of Player N's deck.




\begin{lstlisting}
RepDesc:=proc(Q,r,b,N) local n,c:
  n:=nops(b)/r:
  c:=[seq(op(sort([op(i*r+1..i*r+r,b)],`>`)),i=0..n-1)]:
  c:=remove(t->t=0,c):
  [seq(Q[i],i=1..N-1),[op(Q[N]),op(c)],seq(Q[i],i=N+1..r)]:
end:
\end{lstlisting}




\subsection{Simulating a Game of War}


\subsubsection{Simulating One Move or Round}\hfill


The procedure \textbf{OneMove(P,c,m,WAR,rep)} inputs a list $P$, a list $c$, a positive integer $m$, a war procedure WAR, and a replacement procedure rep. The $i$-th entry of $P$ is a list representing the $i$-th player's hand at the beginning of the move, $c$ lists the cards that have already been placed on the table during this round (i.e. cards accumulated during unresolved wars), and $m$ is the default number of cards to be placed face down during a war. If this round ends the game with no winners it outputs $\{0\}$. If the round ends the game with a tie, then it outputs a set of the players that tied. Otherwise, it outputs a list $Q$ where the $i$-th entry represents each players deck at the end of the round after adding the won cards to the bottom of the winner's deck.


\textbf{OneMove(P,c,m,WAR,rep)} adds $0$ to any empty deck to represent no card, then creates a list $a$ of the first card of each player's deck and a new list $Q$ where the $i$-th entry of $Q$ represents each player's deck after removing their first card. It finds the highest value in $a$ and checks if any other player's had the same card. If the highest card was unique, it places all of the cards at the bottom of the winners deck according to rep. Otherwise, it runs WAR(m,Q,r) to simulate a war. If player's are able to play a card after placing cards face down on the table, it runs $OneMove(Q,b,m,WAR,rep)$ where $Q$ and $b$ are lists such that the $i$-th entry of $Q$ represents the $i$-th player's deck after placing cards on the table, and $b$ lists the cards that have already been placed on the table during this round.


\begin{lstlisting}
OneMove:=proc(P,c,m,WAR,rep) local r, a, b, tempP, i, h, w, Q, output, W:
  r:=nops(P):
  a:=[]:
  b:=[op(c)]:
  tempP:=P:
  for i from 1 to r do
    if P[i]=[] then tempP[i]:=[0]: fi: od:
  a:=[seq(tempP[i][1],i=1..r)]:
  Q:=[seq([op(2..-1,tempP[j])],j=1..r)]:
  h:=max(op(a)):
  w:={SearchAll(h,a)}:
  if nops(w)=1 then
    b:=[op(b),op(a)]:
    output:=rep(Q,r,b,op(w)): 
  else
    W:=WAR(m,Q,r):
    if type(W,list)=true then
      Q:=W[1]:
      b:=[op(b),op(a),op(W[2])]: 
     output:=OneMove(Q,b,m, WAR,rep):
     elif type(W,integer)=true then
       output:=tie:
     else
       output:=W:
     fi: fi:
  output:
  end:
\end{lstlisting}


\subsubsection{Simulating Games of War}\hfill


The procedure \textbf{GenOneGameK(n,k,r,m,WAR,rep,K)} inputs positive integers n,k,r,K, a non-negative integer $m$, war procedure WAR, and replacement procedure rep. It simulates a game of war that is at most $K$ rounds and has $n$ different cards, $k$ copies of each card, and $r$ players. First it generates a random starting deck using $RandDeck(n,k,r)$. Then it runs $OneMove$ either $K$ times or until the game finishes, whichever occurs first. If the game terminates, it outputs a list $[w,i]$ where $w$ is the winner (or set of winners if there is a tie), and $i$ is the number of rounds the game took. If there are no winners, $w={0}.$ If the game does not terminate, the procedure fails. 


\begin{lstlisting}
GenOneGameK:=proc(n,k,r,m, WAR,rep,K) local P, HandSize, i, Q, w:
  P:=RanDeck(n,k,r):
  HandSize:=[seq(nopsP[i],i=1..r)]:


  for i from 1 to K while nops(remove(t->t=0, HandSize))>1 and type(P,list)=true do
    Q:=P:
    HandSize:=[seq(nops(P[i]),i=1..r)]:
    P:=OneMove(P,[],m,WAR,rep): od:


   if i=K+1 then RETURN(FAIL):


   elif nops(remove(t->t=0, HandSize))=1 then w:=max[index](HandSize):


   elif type(P,list)=false then w:=P


   else RETURN(FAIL): fi:
 [w,i-1]:
end:
\end{lstlisting}


The procedure \textbf{GenManyGamesK(n,k,r,m,WAR,rep,K,w,t,M)} inputs positive integers n,k,r,m,K,M, war procedure WAR, replacement procedure rep, and variables w and t. It simulates $M$ random games of war that are at most $K$ rounds and has $n$ different cards, $k$ copies of each card, and $r$ players. It runs $GenOneGameK(n,k,r,m,WAR,rep,K)$ $M$ times, keeping track of which games do not terminate in $K$ moves and the lengths and winners of each terminating game. The procedure outputs a list $[W,T,nw,I]$ where $W$ is a list whose $i$-th entry is the generating function for the duration of terminating games that player $i$ won, $T$ is a list whose $i$-th entry is the generating function for the duration of terminating games where player $i$ tied with someone else, $nw$ is the generating function for the duration of terminating games with no winners, and $I$ is the number of games that did not end in K moves.


\begin{lstlisting}
GenManyGamesK:=proc(n,k,r,m,WAR,rep,K,w,t,M) local f,h,g,i,G,j,nw:
  for i from 1 to r do
    f[i]:=0:  g[i]:=0: od:
  h:=0: nw:=0:


  for i from 1 to M do
    G:=GenOneGameK(n,k,r,m, WAR,rep,K):


  if G=FAIL then h:=h+1
   elif type(G[1],integer)=true then f[G[1]]:=f[G[1]]+w^G[2]
  else
   if nops(G[1])>1 then
     for j in G[1] do
        g[j]:=g[j]+t^G[2]: od:
    else nw:=nw+t^G[2]  fi:
  fi: od:


 [[seq(f[i],i=1..r)],[seq(g[i],i=1..r)],nw,h]:
end:
\end{lstlisting}


The procedure \textbf{GenManyGamesKEDIT(n,k,r,m,WAR,rep,K,w,t,M)} is an edited version of $GenManyGamesK(n,k,r,m,WAR,rep,K,w,t,M)$. It runs similarly to $GenManyGamesK(n,k,r,m,WAR,rep,K,w,t,M)$, but instead outputs a list $[all, win,nw,I]$ where $all$ is the generating function for the duration of all the games that terminated, $win$ is the generating function for the duration of terminating games with one winner,  $nw$ is the generating function for the duration of terminating games with no unique winner, and $I$ is the number of games that did not end in $K$ moves.


\begin{lstlisting}
GenManyGamesKEDIT:=proc(n,k,r,m,WAR,rep,K,t,M) local a, f,h,g,i,G,j,nw:
  a:=0: f:=0: g:=0: h:=0: nw:=0:


  for i from 1 to M do
    G:=GenOneGame(n,k,r,m, WAR,rep,K):


  if G=FAIL then h:=h+1:
   elif type(G[1],integer)=true then
     a:=a+t^G[2]:
     f:=f+t^G[2]:
  else
      a:=a+t^G[2]:
      nw:=nw+t^G[2]: 
  fi: od:


 [a,f,nw,h]:
end:
\end{lstlisting}


The procedure \textbf{FinishGame(H,r,m, WAR,rep)} inputs a list $H$, positive integer $r$ such that $H$ has length $r$, a non-negative integer $m$, a war procedure $WAR$, and a replacement procedure $rep$. It simulates the rest of the game of war where the $i$-th entry of $H$ represents the $i$-th player's deck, $m$ is the default number of cards placed face down during a war, $WAR$ indicates which war procedure is being used, and $rep$ indicates which replacement procedure is being used. The procedure terminates when the number of players with cards left is less than or equal to 1 or a periodic orbit has been found. It outputs a list of length 2, where the first entry says the winner(s) of the game and the second entry is how many rounds the game took if it terminated or the cycle found if it does not terminate.


\textbf{FinishGame(H,r,m, WAR,rep)} runs $OneMove$ until the game terminates or a periodic orbit is found, keeping track of how many rounds have occurred and each player's deck at the start of each round.  It checks for periodic orbits every 10 rounds. A periodic orbit is found after $b>0$ moves if $\exists a$ where $0\leq a < b$ such that each player's deck after $b$ moves is the same as their deck after $a$ moves. If this is true, then the procedure finds the length of the cycle, $L$ by counting the number of unique lists $P$ of each player's deck occurring between moves $a$ and $b$. It then finds the cycle by creating a list of length $L$ where the $i$-th entry is the list of each players deck after $a+i-1$ moves.


\begin{lstlisting}
FinishGame:=proc(H,r,m, WAR,rep) local Q, P, HandSize, moves, cyc, path, N, cycle, lost, output, w:
  P:=H:
  HandSize:=[seq(nops(P[i]),i=1..r)]: 
  moves:=0:
  cyc:=0:
  path:=[P]: 


  while nops(remove(t->t=0, HandSize))>1 and type(P,list)=true do
    P:=OneMove(P,[],m,WAR,rep):
    HandSize:=[seq(nops(P[i]),i=1..r)]:
    moves:=moves+1: 


    if moves mod 10=0 then N:=Search(P,path): 
      if N>0 then
        cyc:=nops({op(N..moves,path)}):
        cycle:=[op(N..N+cyc-1,path)]:
        HandSize:=[seq(nops(P[i]),i=1..r)]:
        lost:={SearchAll(0,HandSize)}:
        w:={seq(i,i=1..r)} minus lost:
       break:  fi: fi: 
    path:=[op(path),P]:  od:


    if cyc>0 then
      output:=[w,cycle]:
    elif nops(remove(t->t=0, HandSize))=1 then
      output:=[max[index](HandSize),moves]:
    elif type(P,list)=false then
      output:=[P,moves]:
    else RETURN(FAIL): fi: 


  output:
end:
\end{lstlisting}


\subsubsection{Finding information on Simple Games of war} \hfill


Here, we define a simple game of war to be a game of war with two players and $2*n$ unique cards. A simple game has the replacement procedure $RepAsc$, $RepDesc$, $RepAllAsc$, or $RepAllDesc$. In other words, player 2 will place the cards $[card1, card2]$ at the bottom of his deck in the same order as player 1 would. In the simple game, we can reduce the number of starting decks we need to look at in order to obtain information on all possible games of war. 


The procedure \textbf{SimpGameInfo(n,rep)} inputs a positive integer $n$ and a replacement procedure $rep$. It outputs a list [[f,C],cycles], where $f$ is the generating function for the duration of terminating games, $C$ is the number of games that lead to cycles, and $cycles$ is the list of unique cycles.


First,  \textbf{SimpGameInfo(n,rep)} reduces the number of starting decks it needs to run. There is no need to distinguish between Player 1 and Player 2 to figure out how all possible games will play out. Because the order that cards are replaced is the same for Player 1 and Player 2, running the simulation where player 1 starts of with the deck H[1] and player 2 starts with the deck H[2] will tell us everything we need to know about the game where player 1 starts with H[2] and player 2 starts with H[1]. Because the player with the (unique) highest card will never lose that card, there will never be a point in the game where player 1 and player 2 swap decks.  So, running simulations where we look at the partitions of 2*n into two parts of size n where player 1 always starts with the highest card, we obtain information on every possible game. (Here, we find half of the unique cycles, but can easily find the other half by switching player 1 and player 2's decks.) Furthermore, we do not need to run simulations for the games where player 1's lowest card is higher than player 2's highest card, as these games obviously end in $n$ moves.


Different hands can enter the same cycle at different points in the cycle, so we need to make sure that we do not count the same cycle multiple times. To do this, first note that if two cycles share any element, then the cycles are the same. Given the way that maple orders the elements in a set, by turning our cycle into a set and then back into a list, we can ensure that the first element is the same regardless of where the cycle starts. Note that this element will always have player 1's first card as 1 because at some point in any cycle player 1 must have 1 as their first card. \textbf{SimpGameInfo(n,rep)} creates a list of these elements and, instead of checking through the whole list to see if a cycle has already been found, it creates sub-lists defined by the second card in player 1's deck. So, if player 1's second card is $b$, the procedure only checks through the elements where player 1's second card is $b$.


\begin{lstlisting}
SimpGameInfo:=proc(n,rep) local Decks, i,o,i1,j1,j2, counter, LedToCyc, cycles, C,N, f, H,G,a,b,k,M,N1,DrZ:
 if rep<>RepAsc and rep<>RepDesc and rep<>RepAllAsc and rep<>RepAllDesc then
    print(`This is not a simple war game. Can't use this procedure.`):
  else
    Decks:=setpartition([seq(i,i=1..2*n)],n):  
   #Right now, Player 1 always starts with 1. It is simpler if he starts with 2*n since he can never lose this card, so we need to turn all of the current 2*n's into 1's and all of the current 1's into 2*n's. 
    Decks:=subs(2*n=DrZ,Decks):  
    Decks:=subs(1=2*n,Decks):
    Decks:=subs(DrZ=1,Decks):
 #It is obvious that if one player's highest card is lower than the other player's lowest card, then they lose the game in n rounds.
    Decks:=remove(t->t=[[2*n, seq(i,i=2*n-n+1..2*n-1)],[seq(i,i=2..2*n-n),1]],Decks):
    


    N1:=n!: 
    N:=nops(Decks): 
    counter:=0:
    LedToCyc:=0:
    cycles:=[]:
    C:=[seq([],i=1..2*n)]: 
    f:=0:
    o:=permute(n):
   
    #Right now each players deck is in a specific order, so we need to look at the various ways to shuffle them.
   for i1 from 1 to N do
    for j1 from 1 to N1 do
      for j2 from 1 to N1 do
        H:=[Decks[i1][1][o[j1]],Decks[i1][2][o[j2]]]: 
      if counter mod 50000 = 0 and counter>0 then                
        print(`Played`, counter, `games`);
        print(nops(cycles), `distinct cycles`);
        print(LedToCyc, `got a cycle`);
          print(f):
       fi:


      counter:=counter+1:


      G:=FinishGame(H,2,1, WARm,rep):
      #Note: m and WAR don't matter since every card is unique.


      if type(G[2],list)=true then 
         #We have a cycle!
         LedToCyc:=LedToCyc+1:
         a:=G[2]:  
         b:=[op({op(a)})][1]: 
         k:=b[1][2]:  
         M:=Search(b,C[k]):


        if M=0 then 
          C:=[seq(C[i],i=1..k-1), [op(C[k]),b], seq(C[i],i=k+1..2*n)]: 
          cycles:=[op(cycles),a]:
         fi: 


      elif type(G[2],integer)=true then
         f:=f+t^G[2]:
        fi:
      od: od: od:
    print(2*nops(cycles), `distinct cycles`):
    print(2*LedToCyc, `got a cycle`):
    print((2*n)!, `total games`):
  fi:


      [[2*f+2*n!^2*t^n,2*LedToCyc],[op(cycles),seq(cycles[i][2],cycles[i][1],i=1..-1)]]:
  end:
\end{lstlisting}


The following separate \textbf{SimpGameInfo(n,rep)} into two procedures \textbf{SimpGameLengths} and \textbf{SimpPerDecks} in order to speed up running the procedures if information on only game lengths or only cycles is needed.




\begin{lstlisting}
SimpPerDecks:=proc(n,rep) local Decks, i, counter, LedToCyc, cycles,o,i1,j1,j2, C,N, H,G,a,b,k,M,N1, DrZ:
 if rep<>RepAsc and rep<>RepDesc and rep<>RepAllAsc and rep<>RepAllDesc then
    print(`This is not a simple war game. Can't use this procedure.`):
  else
    Decks:=setpartition([seq(i,i=1..2*n)],n):  


    #Right now, Player 1 always starts with 1. For later in the code, it is easier if he starts with 2*n since he can never lose this card.
    #We need to turn all of the current 2*n's into 1's and all of the current 1's into 2*n's. Note, that right now the current 1's are always 
    #the first element of P1's deck
    Decks:=subs(2*n=DrZ,Decks):  
    Decks:=subs(1=2*n,Decks):
    Decks:=subs(DrZ=1,Decks): 
    
    #It is obvious that if one player's highest card is lower than the other player's lowest card, then they lose the game in n rounds.
    Decks:=remove(t->t=[[2*n, seq(i,i=2*n-n+1..2*n-1)],[seq(i,i=2..2*n-n),1]],Decks):


    N1:=n!: 
    N:=nops(Decks): 
    counter:=0:
    LedToCyc:=0:
    cycles:=[]:
    C:=[seq([],i=1..2*n)]: 
    o:=permute(n):
   
    #Right now each players deck is in a specific order, so we need to look at the various ways to shuffle them.
   for i1 from 1 to N do
    for j1 from 1 to N1 do
      for j2 from 1 to N1 do
        H:=[Decks[i1][1][o[j1]],Decks[i1][2][o[j2]]]: 
        
      if counter mod 50000 = 0 and counter>0 then                
        print(`Played`, counter, `games`);
        print(nops(cycles), `distinct cycles`);
        print(LedToCyc, `got a cycle`);
       fi:


      counter:=counter+1:


      G:=FinishGame(H,2,1, WARm,rep):
      #Note: m and WAR don't matter since every card is unique.


      if type(G[2],list)=true then 
         #We have a cycle!
         LedToCyc:=LedToCyc+1:
         a:=G[2]:  
         b:=[op({op(a)})][1]: 
         k:=b[1][2]:  
         M:=Search(b,C[k]):


        if M=0 then 
          C:=[seq(C[i],i=1..k-1), [op(C[k]),b], seq(C[i],i=k+1..2*n)]: 
          cycles:=[op(cycles),a]:
         fi: fi:
      od: od: od:
    print(2*nops(cycles), `distinct cycles`):
    print(2*LedToCyc, `got a cycle`):
    print((2*n)!, `total games`):
  fi:


       [op(cycles), seq(cycles[i][2],cycles[i][1],i=1..-1)]:
  end:
\end{lstlisting}


\begin{lstlisting}
SimpGameLengths:=proc(n,rep) local Decks, i,o,i1,j1,j2, counter, LedToCyc,N, f, H,G,N1,DrZ:
 if rep<>RepAsc and rep<>RepDesc and rep<>RepAllAsc and rep<>RepAllDesc then
    print(`This is not a simple war game. Can't use this procedure.`):
  else
    Decks:=setpartition([seq(i,i=1..2*n)],n):  




    #Right now, Player 1 always starts with 1. It is easier if he starts with 2*n since he can never lose this card.
    #We need to turn all of the current 2*n's into 1's and all of the current 1's into 2*n's. Note, that right now the current 1's are always 
    #the first element of P1's deck
    Decks:=subs(2*n=DrZ,Decks):  
    Decks:=subs(1=2*n,Decks):
    Decks:=subs(DrZ=1,Decks):
    
    #It is obvious that if one player's highest card is lower than the other player's lowest card, then they lose the game in n rounds.
    Decks:=remove(t->t=[[2*n, seq(i,i=2*n-n+1..2*n-1)],[seq(i,i=2..2*n-n),1]],Decks):


    N1:=n!: 
    N:=nops(Decks): 
    counter:=0:
    LedToCyc:=0:
    f:=0:
    o:=permute(n):
   
    #Right now each players deck is in a specific order, so we need to look at the various ways to shuffle them.
   for i1 from 1 to N do
    for j1 from 1 to N1 do
      for j2 from 1 to N1 do
        H:=[Decks[i1][1][o[j1]],Decks[i1][2][o[j2]]]: 
      if counter mod 50000 = 0 and counter>0 then                
        print(`Played`, counter, `games`);
        print(nops(cycles), `distinct cycles`);
        print(LedToCyc, `got a cycle`);
          print(f):
       fi:


      counter:=counter+1:


      G:=FinishGame(H,2,1, WARm,rep):
      #Note: m and WAR don't matter since every card is unique.


      if type(G[2],list)=true then 
         #We have a cycle!
         LedToCyc:=LedToCyc+1:


      elif type(G[2],integer)=true then
         f:=f+t^G[2]:
        fi:
      od: od: od:
    print(2*LedToCyc, `got a cycle`):
    print((2*n)!, `total games`):
  fi:


      [2*f+2*n!^2*t^n,2*LedToCyc]:
  end:
\end{lstlisting}










\subsection{The War Graph: Procedures}
The procedures \textbf{WarGraph(n)} and \textbf{ArbRelWG(M)} (short for "arbitrary-relation war graph")
use the GraphTheory package in Maple to construct the \emph{game graph} of war.
In WarGraph, the game is played with a simple $n$-card deck $\{1,2,...,n\}$; when players skirmish,
the winner is always the player of the higher card, and the winner of the game (if there is any) is always the player who began the game with the high-card $n$. For this reason, we have ``modded out" by the symmetry
of exchanging the two players' hands, and allowed only the hands in which the left player Leftie holds the card $n$. 
A position in War--a pair of players' hands $[[L_1, L_2,...,L_m],[R_1,R_2,...,R_\ell]]$--is called \emph{terminal}
if one player (for us, always the right player Rita) has an empty hand $[]$. Apart from terminal positions,
every position in simple War\footnote{That is, war with two players on the linearly ordered deck $\{1,...,n\}$} has two ``children" (two positions that could result from playing one Skirmish), and \textbf{ChildLW} and \textbf{ChildWL} record these two possibilities. The player who wins the skirmish must put the two cards at the bottom of their deck, and there are two possible orders in which to do this, LW (losing-winning) and WL (winning-losing). For example, here is ChildWL:
\begin{lstlisting}
ChildWL:=proc(Hands):
if nops(Hands[1])>0 and nops(Hands[2])>0 then


if Hands[1][1] > Hands[2][1] then
[[op(Hands[1][2..]),Hands[1][1],Hands[2][1]],Hands[2][2..]]:
else
[Hands[1][2..], [op(Hands[2][2..]),Hands[2][1],Hands[1][1]]]:
fi:


else
Hands:
fi:
end:
\end{lstlisting}
The WarGraph is the directed graph in which each node is a position, and directed edges are introduced
from each position to its possible children. It was first studied in Lakshtanov and Roshchina 2011. Here is the code:
\begin{lstlisting}
WarGraph:=proc(n) local positions,perms,i,G,p,splits,H,spot,V,v,S,s,U:
perms:=permute(n): 


positions:=[]:


for p in perms do
 spot:=Search(n,p):
 splits:=seq(convert([p[..i],p[i+1..]],string),i=spot..n):
 positions:=[op(positions),splits]:
od:


G:=Digraph(positions):


for H in positions do
 H:=parse(H):


if nops(H[1]) = 0 or nops(H[2]) = 0 then
else
AddArc(G,{[convert(H,string),convert(ChildWL(H),string)],
[convert(H,string),convert(ChildLW(H),string)]}):
fi:
od:


G:
end:
\end{lstlisting}
See also the War Graph section. 


ArbRelWG is a generalization of WarGraph, in which the winners of skirmishes is determined by
the matrix $M$, rather than standard comparison. The matrix $M$ consists of $0$s and $1$s,
with the rule
$$M[i][j] = \begin{cases} 1 \ \text{if card} \ i \ \text{beats card} \ j \\ 0 \ \text{else.} \end{cases} $$
For example, $M$ could encode the rock-paper-scissors deck:
$$M = \begin{pmatrix} 0 & 0 & 1 \\ 1 & 0 & 0 \\ 0 & 1 & 0 \end{pmatrix}.$$
The procedures ArbRelChildLW and ArbRelChildWL are the analogs of ChildLW and ChildWL
for this generalized version of War. The two major differences between ArbRelWG
and WarGraph are: (1) there is now no notion of a ``high card," so the entire set of $(n+1)!$
positions show up as vertices, and (2) winners are now determined by appeal to the matrix $M$, as you will see:


\begin{lstlisting}
ArbRelWG:=proc(M) local n, positions,perms,i,G,p,splits,H,spot,V,v,S,s,U:
n:=nops(M):


perms:=permute(n): 


positions:=[]:


for p in perms do
splits:=seq(convert([p[..i],p[i+1..]],string),i=0..n):
positions:=[op(positions),splits]:
od:


G:=Digraph(positions):


for H in positions do
H:=parse(H):


if nops(H[1]) = 0 or nops(H[2]) = 0 then
else
 AddArc(G,{[convert(H,string),convert(ArbRelChildWL(H,M),string)],
[convert(H,string),convert(ArbRelChildLW(H,M),string)]}):
fi:
od:


G:
end:
\end{lstlisting}


\section{The War Graph}
\label{sec:graph}


\end{document}
